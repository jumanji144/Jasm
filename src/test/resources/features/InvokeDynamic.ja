class public InvokeDynamic extends java/lang/Object

method public static main ([Ljava/lang/String; a)V

    invokedynamic getPrintStream ()Ljava/io/PrintStream;
        handle
            H_INVOKESTATIC
            InvokeDynamic.bootstrap
            (Ljava/lang/invoke/MethodHandles$Lookup;Ljava/lang/String;Ljava/lang/invoke/MethodType;)Ljava/lang/invoke/CallSite;
        args end

    // call println
    ldc "Hello World"
    invokevirtual java/io/PrintStream.println (Ljava/lang/String;)V

    return
end

method public static bootstrap (Ljava/lang/invoke/MethodHandles$Lookup; lookup,
                                Ljava/lang/String; s1,
                                Ljava/lang/invoke/MethodType; mtype)Ljava/lang/invoke/CallSite;

    new java/lang/invoke/ConstantCallSite // create a new CallSite
    dup

    aload_0 // load the lookup
    ldc type java/lang/System
    ldc "out"
    ldc type java/io/PrintStream
    invokevirtual java/lang/invoke/MethodHandles$Lookup.findStaticGetter (Ljava/lang/Class;Ljava/lang/String;Ljava/lang/Class;)Ljava/lang/invoke/MethodHandle;

    invokevirtual java/lang/invoke/ConstantCallSite.<init> (Ljava/lang/invoke/MethodHandle;)V

    areturn

end